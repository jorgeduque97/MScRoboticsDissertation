MODULE Module1
    VAR socketdev clientSocket;
    VAR socketstatus socketStat;
    PERS bool RUN;

    PROC connectToServer(string IPAddress,num portNo)
        VAR num retryNo:=0;

        !Create client socket
        SocketCreate clientSocket;
        !Connect to the server with IPAddress and portNo specified
        SocketConnect clientSocket,IPAddress,portNo;
        TPErase;
        TPWrite "Connection successful";

    ERROR
        IF ERRNO=ERR_SOCK_TIMEOUT THEN
            WaitTime 1;
            retryNo:=retryNo+1;
            IF retryNo>=3 THEN
                ErrWrite\I,"Connection failed","Trying to connect to online server failed."\RL2:="PP goes to Main to check if server alive."\RL3:="Retries is reset.";
                retryNo:=0;
                WaitTime 5;
                ExitCycle;
            ELSE
                RETRY;
            ENDIF
        ELSEIF ERRNO=ERR_SOCK_CLOSED THEN
            WaitTime 5;
            ExitCycle;
        ELSE
            TPWrite "ERRNo = "\Num:=ERRNO;
            Stop;
        ENDIF
    ENDPROC

    PROC sendData()
        SocketSend clientSocket\Str:="Box in correct position";
        WaitTime 4;
    ENDPROC

    PROC sendCloseConnection()
        SocketSend clientSocket\Str:="Controller 1 stopped";
        WaitTime 2;
    ENDPROC

    PROC main()
        waitTime 0.2;
        !Close previous socket in case of wrong closing process
        SocketClose clientSocket;
        !Get the status of the client socket 
        socketStat:=SocketGetStatus(clientSocket);

        WHILE NOT (socketStat=SOCKET_CONNECTED) DO
            !Wait time before closing the socket
            waitTime 2;
            SocketClose clientSocket;
            !Wait time between closing and opening the socket
            waitTime 2;

            !Connect to the server using the IP address and port number given
            connectToServer "178.79.133.92",5555;
            !Update the status of the client socket
            socketStat:=SocketGetStatus(clientSocket);
        ENDWHILE

        WHILE (socketStat=SOCKET_CONNECTED) DO
            IF sensorsys=1 THEN
                sendData;
            ENDIF
            IF RUN=FALSE THEN
                sendCloseConnection;
                waitTime 2;
                SocketClose clientSocket;
                socketStat:=SocketGetStatus(clientSocket);
                waitTime 2;
            ENDIF
        ENDWHILE
    ENDPROC
ENDMODULE