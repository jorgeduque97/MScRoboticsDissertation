MODULE Module1
    VAR socketdev clientSocket1;
    PERS socketstatus socketStat1;
    VAR string data;
    PERS string mydata;
    PERS bool RUN;
    PERS bool Item;
    PERS bool Ready:=FALSE;
    VAR num i:=0;


    PROC connectToServer(string IPAddress,num portNo)
        VAR num retryNo:=0;

        !Create client socket
        SocketCreate clientSocket1;
        !Connect to the server with IPAddress and portNo specified
        SocketConnect clientSocket1,IPAddress,portNo;
        TPErase;
        TPWrite "Connection successful";

    ERROR
        IF ERRNO=ERR_SOCK_TIMEOUT THEN
            WaitTime 1;
            retryNo:=retryNo+1;
            IF retryNo>=3 THEN
                ErrWrite\I,"Connection failed","Trying to connect to online server failed."\RL2:="PP goes to Main to check if server alive."\RL3:="Retries is reset.";
                retryNo:=0;
                WaitTime 5;
                ExitCycle;
            ELSE
                RETRY;
            ENDIF
        ELSEIF ERRNO=ERR_SOCK_CLOSED THEN
            WaitTime 5;
            ExitCycle;
        ELSE
            TPWrite "ERRNo = "\Num:=ERRNO;
            Stop;
        ENDIF
    ENDPROC

    PROC receiveData()
        waitTime 0.1;
        mydata:="";
        SocketReceive clientSocket1\Str:=data;
        mydata:=data;
        Ready:=TRUE;
        Item:=FALSE;
        WaitTime 0.5;
    ENDPROC

    PROC targetCoordinates()
        waitTime 0.1;
        SocketSend clientSocket1\Str:="Waiting for path specifications";
        !waitTime 1;
    ENDPROC

    PROC sendCloseConnection()
        SocketSend clientSocket1\Str:="End of separation process";
        WaitTime 2;
    ENDPROC

    PROC main()
        !Reset variables
        i:=0;
        
        !Close previous socket in case of wrong closing process
        SocketClose clientSocket1;
        socketStat1:=0;
        
        !Get the status of the client socket 
        socketStat1:=SocketGetStatus(clientSocket1);

        WHILE NOT (socketStat1=SOCKET_CONNECTED) DO
            !Wait time before closing the socket
            waitTime 2;
            SocketClose clientSocket1;
            !Wait time between closing and opening the socket
            waitTime 2;

            !Connect to the server using the IP address and port number given
            connectToServer "178.79.133.92",5555;
            !Update the status of the client socket
            socketStat1:=SocketGetStatus(clientSocket1);
        ENDWHILE

        WHILE (socketStat1=SOCKET_CONNECTED) DO
            IF Item=TRUE THEN
                Ready:=FALSE;
                targetCoordinates;
                waitTime 0.2;
                receiveData;
                i:=i+1;
            ENDIF
            IF i>=12 THEN
                sendCloseConnection;
                waitTime 2;
                SocketClose clientSocket1;
                socketStat1:=SocketGetStatus(clientSocket1);
                waitTime 2;
                Item:=FALSE;
            ENDIF
        ENDWHILE
    ENDPROC
ENDMODULE