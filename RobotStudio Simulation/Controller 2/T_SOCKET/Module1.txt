MODULE Module1
    VAR socketdev clientSocket;
    VAR socketstatus socketStat;
    VAR string data;
    VAR bool okX;
    VAR bool okY;
    VAR bool okZ;
    PERS num Xcoordinates:=0;
    PERS num Ycoordinates:=0;
    PERS num Zcoordinates:=0;
    VAR string XValString;
    VAR string YValString;
    VAR string ZValString;
    PERS bool RUN;


    PROC connectToServer(string IPAddress,num portNo)
        VAR num retryNo:=0;

        !Create client socket
        SocketCreate clientSocket;
        !Connect to the server with IPAddress and portNo specified
        SocketConnect clientSocket,IPAddress,portNo;
        TPErase;
        TPWrite "Connection successful";

    ERROR
        IF ERRNO=ERR_SOCK_TIMEOUT THEN
            WaitTime 1;
            retryNo:=retryNo+1;
            IF retryNo>=3 THEN
                ErrWrite\I,"Connection failed","Trying to connect to online server failed."\RL2:="PP goes to Main to check if server alive."\RL3:="Retries is reset.";
                retryNo:=0;
                WaitTime 5;
                ExitCycle;
            ELSE
                RETRY;
            ENDIF
        ELSEIF ERRNO=ERR_SOCK_CLOSED THEN
            WaitTime 5;
            ExitCycle;
        ELSE
            TPWrite "ERRNo = "\Num:=ERRNO;
            Stop;
        ENDIF
    ENDPROC

    PROC receiveData()
        SocketReceive clientSocket\Str:=data;
        XValString:=StrPart(data,1,14);
        YValString:=StrPart(data,15,9);
        ZValString:=StrPart(data,24,6);
        okX:=StrToVal(XValString,Xcoordinates);
        okY:=StrToVal(YValString,Ycoordinates);
        okZ:=StrToVal(ZValString,Zcoordinates);
        WaitTime 4;
    ENDPROC

    PROC targetCoordinates()
        waitTime 0.1;
        SocketSend clientSocket\Str:="Need target coordinates";
    ENDPROC
    
    PROC sendCloseConnection()
        SocketSend clientSocket\Str:="Controller 2 stopped";
        WaitTime 2;
    ENDPROC

    PROC main()
        waitTime 0.4;
        !Close previous socket in case of wrong closing process
        SocketClose clientSocket;
        !Get the status of the client socket 
        socketStat:=SocketGetStatus(clientSocket);

        WHILE NOT (socketStat=SOCKET_CONNECTED) DO
            !Wait time before closing the socket
            WaitTime 2;
            SocketClose clientSocket;
            !Wait time between closing and opening the socket
            WaitTime 2;

            !Connect to the server using the IP address and port number given
            connectToServer "178.79.133.92",5555;
            !Update the status of the client socket
            socketStat:=SocketGetStatus(clientSocket);
        ENDWHILE

        WHILE (socketStat=SOCKET_CONNECTED) DO
            IF sensorsys = 1 THEN
                targetCoordinates;
                receiveData;
            ENDIF
            IF RUN = FALSE THEN
                sendCloseConnection;
                waitTime 2;
                SocketClose clientSocket;
                socketStat:=SocketGetStatus(clientSocket);
                waitTime 2;
                !Reset of target coordinates variables
                Xcoordinates:=0;
                Ycoordinates:=0;
                Zcoordinates:=0;
            ENDIF
        ENDWHILE
    ENDPROC
ENDMODULE